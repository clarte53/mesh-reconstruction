#include "ProceduralRenderingHelpers.cginc"

// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel ClearVertexBuffer_1022_1_1
#pragma kernel ClearVertexBuffer_512_2_1
#pragma kernel ClearVertexBuffer_719_1_1
#pragma kernel ClearVertexBuffer_990_1_1
#pragma kernel ClearVertexBuffer_741_1_1
#pragma kernel ClearVertexBuffer_256_4_1
#pragma kernel ClearVertexBuffer_64_16_1
#pragma kernel ClearVertexBuffer_32_32_1

#pragma kernel FillVertexBuffer_512_2_1
#pragma kernel FillVertexBuffer_256_4_1
#pragma kernel FillVertexBuffer_21_47_1
#pragma kernel FillVertexBuffer_128_8_1
#pragma kernel FillVertexBuffer_64_16_1
#pragma kernel FillVertexBuffer_32_32_1

// Fallback kernels
#pragma kernel ClearVertexBuffer_1_1_1
#pragma kernel FillVertexBuffer_1_1_1

Texture2D<float4> Input_vertexTexture;
matrix<float, 4, 4> pointsToClippingBox;
int width;
int clear_width;
int clear_height;

RWStructuredBuffer<Vert> vertexBuffer;

float maxEdgeLength;

float startFadeAngle = 1.1;
float endFadeAngle = 1.4;

void ClearBuffer(uint3 id)
{
	//The mesh read back code treats verts with a -1 for w position
	//as blank. Thats why Im using -1s.

	Vert vert;
	vert.position = float4(-1, -1, -1, -1);

	vert.uv = float2(0, 0);

	int index = id.x + id.y * clear_width + id.z * clear_width * clear_height;

	vertexBuffer[index] = vert;
}

void FillBuffer(uint3 id)
{
	float epsilon = 0.00001f;
	float confidence = 1.0;

	float4 p1 = Input_vertexTexture[uint2(id.x + 0, id.y + 0)];
	float4 p2 = Input_vertexTexture[uint2(id.x + 0, id.y + 1)];
	float4 p3 = Input_vertexTexture[uint2(id.x + 1, id.y + 0)];
	float4 p4 = Input_vertexTexture[uint2(id.x + 1, id.y + 1)];
	
	bool discard_triangle = false;

	// Test whether the p1 is in clipping box
	discard_triangle = ShouldPointBeDiscarded(pointsToClippingBox, p1.xyz);
	
	if (!discard_triangle)
	{
		// Test whether one of the vertices is invalid (reported as at the origin)
		if (length(p1.xyz) < epsilon || length(p2.xyz) < epsilon || length(p3.xyz) < epsilon || length(p4.xyz) < epsilon)
		{
			discard_triangle = true;
		}
	}

	if (!discard_triangle)
	{
		// Test depth gradient against maxEdgeLength threshold
		if (abs(p1.z - p2.z) > maxEdgeLength || abs(p1.z - p3.z) > maxEdgeLength || abs(p2.z - p4.z) > maxEdgeLength || abs(p3.z - p4.z) > maxEdgeLength)
		{
			discard_triangle = true;
		}
	}

	if (discard_triangle)
	{
		p1 = float4(0.0, 0.0, 0.0, 1.0);
		p4 = p3 = p2 = p1;
	}
	else
	{
		float4 p1p2 = p2 - p1;
		float4 p1p3 = p3 - p1;

		// Face normal
		float3 n = normalize(cross(p1p2.xyz, p1p3.xyz));

		// Projection direction
		float3 v = -normalize(p1.xyz);

		// Compute angle between n and v
		float scal = dot(n, v);
		float angle = acos(scal);

		if (angle < startFadeAngle)
		{
			// Maximum confidence if face is globally facing the projection direction
			confidence = 1.0;
		}
		else
		{
			// Linearly decrease confidence passed startFadeAngle, to reach 0 at endFadeAngle
			float r = (angle - startFadeAngle) / (endFadeAngle - startFadeAngle);

			if (r > 1)
			{
				r = 1;
			}

			confidence = 1.0 - r;
		}
	}
	

	int index = id.x + id.y * width;

	vertexBuffer[6 * index + 0] = CreateVertex(p1, confidence);
	vertexBuffer[6 * index + 1] = CreateVertex(p4, confidence);
	vertexBuffer[6 * index + 2] = CreateVertex(p3, confidence);

	vertexBuffer[6 * index + 3] = CreateVertex(p1, confidence);
	vertexBuffer[6 * index + 4] = CreateVertex(p2, confidence);
	vertexBuffer[6 * index + 5] = CreateVertex(p4, confidence);
}


[numthreads(1022, 1, 1)]
void ClearVertexBuffer_1022_1_1(uint3 id : SV_DispatchThreadID)
{
	ClearBuffer(id);
}

[numthreads(512, 2, 1)]
void ClearVertexBuffer_512_2_1(uint3 id : SV_DispatchThreadID)
{
	ClearBuffer(id);
}

[numthreads(719, 1, 1)]
void ClearVertexBuffer_719_1_1(uint3 id : SV_DispatchThreadID)
{
	ClearBuffer(id);
}

[numthreads(990, 1, 1)]
void ClearVertexBuffer_990_1_1(uint3 id : SV_DispatchThreadID)
{
	ClearBuffer(id);
}

[numthreads(741, 1, 1)]
void ClearVertexBuffer_741_1_1(uint3 id : SV_DispatchThreadID)
{
	ClearBuffer(id);
}

[numthreads(256, 4, 1)]
void ClearVertexBuffer_256_4_1(uint3 id : SV_DispatchThreadID) {
	ClearBuffer(id);
}

[numthreads(64, 16, 1)]
void ClearVertexBuffer_64_16_1(uint3 id : SV_DispatchThreadID) {
	ClearBuffer(id);
}

[numthreads(32, 32, 1)]
void ClearVertexBuffer_32_32_1(uint3 id : SV_DispatchThreadID) {
	ClearBuffer(id);
}


[numthreads(512, 2, 1)]
void FillVertexBuffer_512_2_1(uint3 id : SV_DispatchThreadID)
{
	FillBuffer(id);
}

[numthreads(256, 4, 1)]
void FillVertexBuffer_256_4_1(uint3 id : SV_DispatchThreadID)
{
	FillBuffer(id);
}

[numthreads(21, 47, 1)]
void FillVertexBuffer_21_47_1(uint3 id : SV_DispatchThreadID)
{
	FillBuffer(id);
}

[numthreads(128, 8, 1)]
void FillVertexBuffer_128_8_1(uint3 id : SV_DispatchThreadID)
{
	FillBuffer(id);
}

[numthreads(64, 16, 1)]
void FillVertexBuffer_64_16_1(uint3 id : SV_DispatchThreadID)
{
	FillBuffer(id);
}

[numthreads(32, 32, 1)]
void FillVertexBuffer_32_32_1(uint3 id : SV_DispatchThreadID)
{
	FillBuffer(id);
}

// Fallback kernels
[numthreads(1, 1, 1)]
void ClearVertexBuffer_1_1_1(uint3 id : SV_DispatchThreadID)
{
	ClearBuffer(id);
}

[numthreads(1, 1, 1)]
void FillVertexBuffer_1_1_1(uint3 id : SV_DispatchThreadID)
{
	FillBuffer(id);
}